__xx_copy_1password_field() {
    source ~/.zshrc.d/xx_functions/__xx_notify
    source ~/.zshrc.d/xx_functions/__xx_refresh_1password_keychain_entry
    source ~/.zshrc.d/xx_functions/__xx_tempfile
    source ~/.zshrc.d/xx_functions/__xx_get_1password_item_fields

    local ITEM="${1}"

    if [ -z "${TMUX}" ]; then
        __xx_notify "#[fg=#d20f39,bold]This option is only available inside tmux!#[default]" 1000
        return 1
    fi

    # Get fields from cache or fetch fresh (with notifications on cache miss)
    local FIELDS=$(__xx_get_1password_item_fields "${ITEM}" false true)

    if [ -z "${FIELDS}" ]; then
        __xx_notify "#[fg=#d20f39,bold]1password item '${ITEM}' has no copyable fields#[default]" 1000
        return 0
    fi

    # Write fields to temp file for gum to read
    local TEMP_FIELDS=$(__xx_tempfile "xx-1password-fields")
    local TEMP_RESULT=$(__xx_tempfile "xx-1password-field")
    echo "${FIELDS}" > "${TEMP_FIELDS}"

    # Calculate popup dimensions
    local field_count=$(echo "${FIELDS}" | wc -l | tr -d ' ')
    local max_field_length=$(echo "${FIELDS}" | awk '{print length}' | sort -rn | head -1)
    local pane_width=$(tmux display-message -p '#{pane_width}')
    local pane_height=$(tmux display-message -p '#{pane_height}')

    local popup_width=$(( max_field_length + 30 ))
    if [ $popup_width -lt 40 ]; then
        popup_width=40
    elif [ $popup_width -gt $((pane_width - 4)) ]; then
        popup_width=$((pane_width - 4))
    fi

    local popup_height=$(( field_count + 8 ))
    if [ $popup_height -lt 10 ]; then
        popup_height=10
    elif [ $popup_height -gt $((pane_height - 4)) ]; then
        popup_height=$((pane_height - 4))
    fi

    # Show gum choose in popup
    tmux display-popup \
        -h ${popup_height} \
        -w ${popup_width} \
        -S 'fg=#f5e0dc' \
        -T '#[align=absolute-centre] Select Field to Copy #[align=right]' \
        -E "cat '${TEMP_FIELDS}' | gum choose \
                --header='Choose a field from ${ITEM}:' \
                --header.foreground='#f5e0dc' \
                --header.bold \
                --cursor.foreground='#f5e0dc' \
                --item.foreground='#e0def4' \
                --selected.foreground='#313244' \
                --selected.background='#f5e0dc' > '${TEMP_RESULT}'"

    # Check if a field was selected and copy it
    if [ -s "${TEMP_RESULT}" ]; then
        local SELECTED_FIELD=$(cat "${TEMP_RESULT}")
        rm -f "${TEMP_RESULT}"
        rm -f "${TEMP_FIELDS}"

        __xx_refresh_1password_keychain_entry > /dev/null 2>&1
        local FIELD_VALUE=$(op item get "${ITEM}" --fields "label=${SELECTED_FIELD}" --reveal 2>/dev/null)

        if [ -n "${FIELD_VALUE}" ]; then
            printf '%s' "${FIELD_VALUE}" | pbcopy
            __xx_notify "Copied '${SELECTED_FIELD}' from 1password item '${ITEM}'" 1000
        else
            __xx_notify "#[fg=#d20f39,bold]Failed to retrieve field '${SELECTED_FIELD}'#[default]" 1000
        fi
    else
        rm -f "${TEMP_RESULT}"
        rm -f "${TEMP_FIELDS}"
    fi
}
