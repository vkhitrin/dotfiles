__xx_copy_1password_field() {
    source ~/.zshrc.d/xx_functions/__xx_notify
    source ~/.zshrc.d/xx_functions/__xx_refresh_1password_keychain_entry
    source ~/.zshrc.d/xx_functions/__xx_tempfile
    source ~/.zshrc.d/xx_functions/__xx_get_1password_item_fields

    local ITEM="${1}"

    if [ -z "${TMUX}" ]; then
        __xx_notify "#[fg=#d20f39,bold]This option is only available inside tmux!#[default]" 1000
        return 1
    fi

    local FIELDS=$(__xx_get_1password_item_fields "${ITEM}" false true)

    if [ -z "${FIELDS}" ]; then
        __xx_notify "#[fg=#d20f39,bold]1password item '${ITEM}' has no copyable fields#[default]" 1000
        return 0
    fi

    local DISPLAY_NAME="${ITEM}"

    local TEMP_FIELDS=$(__xx_tempfile "xx-1password-fields")
    local TEMP_RESULT=$(__xx_tempfile "xx-1password-field")
    echo "${FIELDS}" > "${TEMP_FIELDS}"

    local FIELD_COUNT=$(echo "${FIELDS}" | wc -l | tr -d ' ')
    local MAX_FIELD_LENGTH=$(echo "${FIELDS}" | awk '{print length}' | sort -rn | head -1)
    local DISPLAY_NAME_LENGTH=${#DISPLAY_NAME}
    local HEADER_TEXT_LENGTH=$(( DISPLAY_NAME_LENGTH + 24 ))  # "Choose a field from " (22) + ": " (2)
    local PANE_WIDTH=$(tmux display-message -p '#{pane_width}')
    local PANE_HEIGHT=$(tmux display-message -p '#{pane_height}')

    local CONTENT_WIDTH=${MAX_FIELD_LENGTH}
    if [ ${HEADER_TEXT_LENGTH} -gt ${CONTENT_WIDTH} ]; then
        CONTENT_WIDTH=${HEADER_TEXT_LENGTH}
    fi

    local POPUP_WIDTH=$(( CONTENT_WIDTH + 10 ))  # Add padding for cursor, borders, margins
    if [ ${POPUP_WIDTH} -lt 50 ]; then
        POPUP_WIDTH=50
    elif [ ${POPUP_WIDTH} -gt $((PANE_WIDTH - 4)) ]; then
        POPUP_WIDTH=$((PANE_WIDTH - 4))
    fi

    local POPUP_HEIGHT=$(( FIELD_COUNT + 5 ))
    if [ ${POPUP_HEIGHT} -lt 8 ]; then
        POPUP_HEIGHT=8
    elif [ ${POPUP_HEIGHT} -gt $((PANE_HEIGHT - 4)) ]; then
        POPUP_HEIGHT=$((PANE_HEIGHT - 4))
    fi

    tmux display-popup \
        -h ${POPUP_HEIGHT} \
        -w ${POPUP_WIDTH} \
        -S 'fg=#f5e0dc' \
        -T '#[align=absolute-centre] Select Field to Copy #[align=right]' \
        -E "cat '${TEMP_FIELDS}' | gum choose \
                --header='Choose a field from ${DISPLAY_NAME}:' \
                --header.foreground='#f5e0dc' \
                --header.bold \
                --cursor.foreground='#f5e0dc' \
                --item.foreground='#e0def4' \
                --selected.foreground='#313244' \
                --selected.background='#f5e0dc' > '${TEMP_RESULT}'"

    if [ -s "${TEMP_RESULT}" ]; then
        local SELECTED_FIELD=$(cat "${TEMP_RESULT}")
        rm -f "${TEMP_RESULT}"
        rm -f "${TEMP_FIELDS}"

        local FIELD_VALUE=$(op item get "${ITEM}" --fields "label=${SELECTED_FIELD}" --reveal 2>/dev/null)

        if [ -n "${FIELD_VALUE}" ]; then
            printf '%s' "${FIELD_VALUE}" | pbcopy
            __xx_notify "Copied '${SELECTED_FIELD}' from 1password item '${DISPLAY_NAME}'" 1000
        else
            __xx_notify "#[fg=#d20f39,bold]Failed to retrieve field '${SELECTED_FIELD}'#[default]" 1000
        fi
    else
        rm -f "${TEMP_RESULT}"
        rm -f "${TEMP_FIELDS}"
    fi
}
