__xx_get_jira_releases() {
    jira releases list | sed -r 's/\x1B\[[0-9;]*m//g' | gawk '
/^[0-9]/ || /^ID/ {
    if (rec) print rec;
    rec = $0;
    next;
}
{
    rec = rec " " $0;
}
END {
    if (rec) print rec;
}
' | gawk '
BEGIN {
    printf "%-7s %-45s %-8s %s\n", "ID", "NAME", "RELEASED", "DESCRIPTION";
}
$1 == "ID" { next; }

{
    id = $1;
    title = "";
    desc = "";
    released = "";
    status_pos = 0;

    # Find the position of the true/false field
    for (i = 1; i <= NF; i++) {
        if ($i == "true" || $i == "false") {
            released = ($i == "true") ? "released" : "not released";
            status_pos = i;
            break;
        }
    }

    # Extract title (fields between ID and status)
    for (i = 2; i < status_pos; i++) {
        title = title $i " ";
    }

    # Extract description (fields after status)
    for (i = status_pos + 1; i <= NF; i++) {
        desc = desc $i " ";
    }

    gsub(/[ \t]+$/, "", title);
    gsub(/[ \t]+$/, "", desc);

    printf "%-7s %-45s %-12s %s\n", id, title, released, desc;
}' | (read -r HEADER; echo "${HEADER}"; sort -rn -k1)
}
